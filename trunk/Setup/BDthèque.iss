; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Languages]
Name: fr; MessagesFile: compiler:French-15-4.1.8.isl

[Setup]
AppVersion={#GetFileVersion("D:\Bureautique\BDthèque\BD.exe")}
AppName=BDthèque
AppVerName=BDthèque {#SetupSetting("AppVersion")}
AppMutex=TetramCorpBDMutex
AppPublisher=Teträm Corp
AppPublisherURL=http://www.tetram.info
AppSupportURL=mailto:dev@tetram.info
AppUpdatesURL=http://www.tetram.info
UninstallDisplayIcon={app}\BD.exe
DefaultDirName={pf}\Teträm Corp\BDthèque
DefaultGroupName=Teträm Corp\BDthèque
PrivilegesRequired=poweruser
AppCopyright=Teträm Corp © 1997-2004
AppID={{A86E29B5-D1EE-431F-A5BF-E4A10D36CBDC}}
LicenseFile=G:\Programmation\MEDIA.KIT\BDthèque 1.0\Setup\LicenceFreeWare.rtf
WindowVisible=true
BackColor=$8080ff
BackColor2=clPurple
BackColorDirection=toptobottom
WizardImageBackColor=clBlack
OutputBaseFilename=BDTheque-setup-{#SetupSetting("AppVersion")}
MinVersion=4.1.1998,5.0.2195
EnableDirDoesntExistWarning=true
AllowUNCPath=false
AppendDefaultDirName=false
ShowLanguageDialog=yes
InternalCompressLevel=normal
SolidCompression=true
Compression=lzma/normal
VersionInfoVersion={#SetupSetting("AppVersion")}
VersionInfoCompany=Teträm Corp
VersionInfoTextVersion={#SetupSetting("AppVersion")}

[Tasks]
Name: desktopicon; Description: Créer un raccourci sur le &bureau; GroupDescription: Raccourcis supplémentaires:
Name: interneticon; Description: Créer un raccourci vers le site de Teträm Corp; GroupDescription: Raccourcis supplémentaires:

[Files]
Source: D:\Bureautique\BDthèque\BD.exe; DestDir: {app}
Source: D:\Bureautique\BDthèque\BDPic.dll; DestDir: {app}; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\fbembed.dll; DestDir: {app}
Source: D:\Bureautique\BDthèque\firebird.msg; DestDir: {app}; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\ib_util.dll; DestDir: {app}
Source: D:\Bureautique\BDthèque\UDF\BDT_UDF.dll; DestDir: {app}\UDF
Source: D:\Bureautique\BDthèque\Base vide\BD.GDB; DestDir: {app}; Flags: onlyifdoesntexist ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Acceuil.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Fond.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\RepInitiales.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Repertoire.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FicheAlbum.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FichePersonne.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\FicheSerie.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Manquants.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Previsions.html; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\Styles.css; DestDir: {app}\WebServer; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\Acceuil.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\logo.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: D:\Bureautique\BDthèque\WebServer\graphics\fondbdd.jpg; DestDir: {app}\WebServer\graphics; Flags: ignoreversion
Source: What's New.txt; DestDir: {app}; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: {app}\BD.url; Section: InternetShortcut; Key: URL; String: http://www.tetram.info; Tasks: interneticon

[Icons]
Name: {group}\BDthèque; Filename: {app}\BD.exe; IconIndex: 0
Name: {group}\BDthèque on the Web; Filename: {app}\BD.url; Tasks: interneticon
Name: {group}\Désinstaller BDthèque; Filename: {uninstallexe}
Name: {userdesktop}\BDthèque; Filename: {app}\BD.exe; Tasks: desktopicon; IconIndex: 0

[Run]
Filename: {app}\BD.exe; Description: Lancer BDthèque; Flags: nowait postinstall skipifsilent
Filename: {app}\What's New.txt; Flags: nowait shellexec skipifdoesntexist postinstall skipifsilent

[UninstallDelete]
Type: files; Name: {app}\BD.url
Type: files; Name: {app}\BD.ini
Type: files; Name: {app}\*.lck
Type: files; Name: {app}\firebird.log

[LangOptions]
LanguageName=French
LanguageID=$040C

[_ISTool]
UseAbsolutePaths=false

[InstallDelete]
Name: {app}\UDF\VDO_UDF.dll; Type: files
Name: {app}\BDws.dll; Type: files

[Code]
var
  CanUpdate, IsUpdate: Boolean;
  Textes, Valeurs: TArrayOfString;

procedure InitializeWizard();
var
  CurVersion: string;
begin
  SetArrayLength(Textes, 2);
  Textes[0] := 'Mettre à jour la version existante';
  Textes[1] := 'Installer de nouveau';
  SetArrayLength(Valeurs, 2);
  Valeurs[0] := '1';
  Valeurs[1] := '0';
  IsUpdate := False;
  CanUpdate := RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{A86E29B5-D1EE-431F-A5BF-E4A10D36CBDC}_is1', 'DisplayVersion', CurVersion) and (CurVersion <> '');
end;

function ScriptDlgPages(CurPage: Integer; BackClicked: Boolean): Boolean;
var
  Next: Boolean;
begin
  if CanUpdate and (
        (not BackClicked and (CurPage = wpLicense))
     or (BackClicked and ((CurPage = wpSelectTasks) or (CurPage = wpSelectDir)))
                    ) then begin
    ScriptDlgPageOpen();
    ScriptDlgPageSetCaption('BDthèque est déjà installé');
    ScriptDlgPageSetSubCaption1('Le programme d''installation à détecter que BDthèque est déjà installé sur cet ordinateur.');
    ScriptDlgPageSetSubCaption2('Selectionner l''action que vous voulez effectuer et cliquer sur Suivant pour continuer.'#13#10#13#10#13#10'ATTENTION: Si vous avez personnalisé le WebServer, pensez à sauvegarder votre répertoire WebServer avant de procéder à la mise à jour: son contenu sera remplacé.');

    Next := InputOptionArray(Textes, Valeurs, True, False);
    while Next and (Valeurs[0] + Valeurs[1] = '00') do begin
      MsgBox('Vous devez sélectionner une action.', mbError, MB_OK);
      Next := InputOptionArray(Textes, Valeurs, True, False);
    end;
    { See NextButtonClick and BackButtonClick: return True if the click should be allowed }
    IsUpdate := Valeurs[0] = '1';
    if not BackClicked then
      Result := Next
    else
      Result := not Next;
    { Close the wizard page. Do a FullRestore only if the click (see above) is not allowed }
    ScriptDlgPageClose(not Result);
  end else begin
    Result := True;
  end;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, False);
end;

function BackButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, True);
end;

function SkipCurPage(CurPage: Integer): Boolean;
begin
  Result := ((CurPage = wpSelectDir) or (CurPage = wpSelectProgramGroup)) and CanUpdate and IsUpdate;
end;
